// SPDX-License-Identifier: Unlicense
pragma solidity ^0.8.0;

import "./ERC721Enumerable.sol";
import "./Ownable.sol";

contract NFT is ERC721Enumerable, Ownable {
    using Strings for uint256;
    string public baseURI;
    string public baseExtension = ".json";
    uint256 public cost;
    uint256 public maxSupply;
    uint256 public allowMintingOn;
    uint256 public maxMintingAmount;
    bool public mintingPaused;
    mapping(address => bool) public whiteList;

    event Mint(uint256 amount, address minter);
    event Withdraw(uint256 amount, address owner);

    constructor(
        string memory _name,
        string memory _symbol,
        uint256 _cost,
        uint256 _maxSupply,
        uint256 _maxMintingAmount,
        uint256 _allowMintingOn,
        string memory _baseURI
    ) ERC721(_name, _symbol) {
        cost = _cost;
        maxSupply = _maxSupply;
        maxMintingAmount = _maxMintingAmount;
        allowMintingOn = _allowMintingOn;
        baseURI = _baseURI;
    }

    function addMintersToWhitelist(address _whitelistAddress) public onlyOwner {
        whiteList[_whitelistAddress] = true;
    }

    function mint(uint256 _mintAmount) public payable {
        require(whiteList[msg.sender] == true);
        require(block.timestamp >= allowMintingOn);
        require(_mintAmount > 0);
        require(_mintAmount <= maxMintingAmount);
        require(msg.value >= cost * _mintAmount);
        require(!mintingPaused, "minting is currently paused.");

        uint256 supply = totalSupply();

        require(supply + _mintAmount <= maxSupply);

        for (uint256 i = 1; i <= _mintAmount; i++) {
            _safeMint(msg.sender, supply + i);
        }

        emit Mint(_mintAmount, msg.sender);
    }

    function mintingPause() public onlyOwner {
        if (!mintingPaused) {
            mintingPaused = true;
        } else {
            mintingPaused = false;
        }
    }

    function tokenURI(
        uint256 _tokenId
    ) public view virtual override returns (string memory) {
        require(_exists(_tokenId), "token id does not exist");
        return (
            string(
                abi.encodePacked(baseURI, _tokenId.toString(), baseExtension)
            )
        );
    }

    function walletOfOwner(
        address _owner
    ) public view returns (uint256[] memory) {
        uint256 ownerTokenAccount = balanceOf(_owner);
        uint256[] memory tokenIds = new uint256[](ownerTokenAccount);
        for (uint256 i; i < ownerTokenAccount; i++) {
            tokenIds[i] = tokenOfOwnerByIndex(_owner, i);
        }
        return tokenIds;
    }

    function withdraw() public onlyOwner {
        uint256 balance = address(this).balance;

        (bool success, ) = payable(msg.sender).call{value: balance}("");
        require(success);

        emit Withdraw(balance, msg.sender);
    }

    function setCost(uint256 _newCost) public onlyOwner {
        cost = _newCost;
    }
}
